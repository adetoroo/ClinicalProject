*****************************************
Filename: SDTM_AE.sas
Author: Moses Adetoro
Date: 12 MAY 2019
SAS: SAS 9.4 (TS2M0)
Platform: Windows XP
Project/Study: 765/15
Description: <To develop the SDTM AE Datasets>
Input: Raw.Adverse_Effect
Output: SDTM.AE
Macros Used: <No macros used>
------------------------------------------
MODIFICATION HISTORY:

<DD-MON-YYYY>, <Firstname Lastname>

<Description>
******************************************;
libname Raw "C:\PROJECT\QPS3\RAW";
libname SDTM "C:\PROJECT\QPS3\SDTM";

/*DERIVE THE SDTM AE VARIABLE*/;

DATA AE1;
SET RAW.ADVERSE_EFFECT;
LENGTH AESEV $10. USUBJID $50.;
STUDYID="765/15";
Domain="AE";
SUBJID=strip(SUBJID);
SITEID="001";
USUBJID=STRIP(STUDYID)||"-"||STRIP(SITEID)||"-"||STRIP(SUBJID);
AETERM=STRIP(EVENT);
AEDECOD=STRIP(DECOD);
AELLT="";
AELLTCD="";
AEPTCD="";
AEHLT="";
AEHLTCD="";
AEHLGT="";
AEHLGTCD="";
AEBODSYS=BODSYS;
AEBDSYCD="";
AESOC="";
AESOCCD="";
AESEV=UPCASE(SEV);
AESER=UPCASE(SUBSTR(SER,1,1));
AEACN=UPCASE(ACTT);
AEACNOTH=COCN;
AEREL=CAU;
AEOUT=UPCASE(OUTCOME);
AESTDTC=PUT(STDT, IS8601DA.)||"T"||PUT(STTM, TOD8.);
AESTDTN=DATEPART(INPUT(AESTDTC, IS8601DT.));
FORMAT AESTDTN DATE9.;
AEENDTC=PUT(STOPDT, IS8601DA.)||"T"||PUT(STOPTM, TOD8.);
AEENDTN=DATEPART(INPUT(AEENDTC, IS8601DT.));
FORMAT AEENDTN DATE9.;
RUN;

/*DERIVED AESTDY AND AEENDY REFERENCING RFSTDTC FROM DM DATASETS*/;

DATA DM;
SET SDTM.DM;
RFSTDTN=DATEPART(INPUT(RFSTDTC, IS8601DT.));
FORMAT RFSTDTN DATE9.;
KEEP USUBJID RFSTDTC RFSTDTN;
RUN;

PROC SORT DATA=AE1;
BY USUBJID;
RUN;

PROC SORT DATA=DM;
BY USUBJID;
RUN;


DATA AE_DM;
MERGE AE1(IN=A) DM(IN=B) ;
BY USUBJID;
IF A AND B;
RUN;

DATA AE3;
SET AE_DM;
IF RFSTDTN > . AND AESTDTN > . THEN DO;
IF AESTDTN < RFSTDTN THEN AESTDY=AESTDTN-RFSTDTN;
ELSE AESTDY=AESTDTN-RFSTDTN+1;
END;


IF RFSTDTN > . AND AEENDTN > . THEN DO;
IF AEENDTN < RFSTDTN THEN AEENDY=AEENDTN-RFSTDTN;
ELSE AEENDY=AEENDTN-RFSTDTN+1;
END;


RUN;

DATA AE4;
SET AE3;
AEDUR1=AEENDY-AESTDY+1;
AEDUR=COMPRESS("P"||PUT(AEDUR1, BEST.)||"D");
RUN;

PROC SORT DATA=AE4;
BY USUBJID AESTDTN;
RUN;

DATA AE5;
SET AE4;
BY USUBJID AESTDTN;
IF FIRST.USUBJID=1 THEN AESEQ=1;
ELSE AESEQ+1;
RUN;

DATA AE_FINAL;
SET AE5;
KEEP STUDYID DOMAIN USUBJID AESEQ AETERM AELLT AELLTCD AEDECOD 
AEPTCD AEHLT AEHLTCD AEHLGT AEHLGTCD AEBODSYS AEBDSYCD AESOC AESOCCD 
AESEV AESER AEACN AEACNOTH AEREL AEOUT AESTDTC AEENDTC AESTDY AEENDY AEDUR
;
RUN;

PROC SQL;
CREATE TABLE AE_FINAL1 AS
SELECT
STUDYID		"Study Identifier"									LENGTH=8,
DOMAIN		"Domain Abbreviation"								LENGTH=2,
USUBJID		"Unique Subject Identifier"							LENGTH=50,
AESEQ		"Sequence Number"									LENGTH=8,
AETERM		"Reported Term for the Adverse Event"				LENGTH=200,
AELLT		"Lowest Level Term"									LENGTH=100,
AELLTCD		"Lowest Level Term Code"							LENGTH=8,
AEDECOD		"Dictionary-Derived Term"							LENGTH=200,
AEPTCD		"Preferred Term Code"								LENGTH=8,
AEHLT		"High Level Term"									LENGTH=100,
AEHLTCD		"High Level Term Code"								LENGTH=8,
AEHLGT		"High Level Group Term"								LENGTH=100,
AEHLGTCD	"High Level Group Term Code"						LENGTH=8,
AEBODSYS	"Body System or Organ Class"						LENGTH=200,
AEBDSYCD	"Body System or Organ Class Code"					LENGTH=8,
AESOC		"Primary System Organ Class"						LENGTH=200,
AESOCCD		"Primary System Organ Class Code"					LENGTH=8,
AESEV		"Severity/Intensity"								LENGTH=10,
AESER		"Serious Event"										LENGTH=2,
AEACN		"Action Taken with Study Treatment"					LENGTH=16,
AEACNOTH	"Other Action Taken"								LENGTH=200,
AEREL		"Causality"											LENGTH=20,
AEOUT		"Outcome of Adverse Event"							LENGTH=40,
AESTDTC		"Start Date/Time of Adverse Event"					LENGTH=25,
AEENDTC		"End Date/Time of Adverse Event"					LENGTH=25,
AESTDY		"Study Day of Start of Adverse Event"				LENGTH=8,
AEENDY		"Study Day of End of Adverse Event"					LENGTH=8,
AEDUR		"Duration of Adverse Event"							LENGTH=25
FROM AE_FINAL;

QUIT;

DATA SDTM.AE(LABEL=Adverse Events);
SET AE_FINAL1;
RUN;

LIBNAME XPT XPORT "C:\PROJECT\QPS3\SDTM\XPT\XPT_SDTM\AE.XPT";

DATA XPT.AE;
SET AE_FINAL1;
RUN;
